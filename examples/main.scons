#!/usr/bin/python
#
# Copyright 2010 The Native Client SDK Inc. All Rights Reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

""" Main scons script for example builds.
"""

import commands
import os
import sys
import SCons.Script

environment_list = []

AddOption('--target',
          dest = 'target',
          type = 'string',
          nargs = 1,
          action = 'store',
          metavar = 'TARGET',
          default = 'x86',
          help = 'target architecture (x64 or x86)')

base_env = Environment(
    tools = ['component_setup'],
    CCFLAGS = ['-Wall', '-Werror', '-Wno-long-long', '-pthread'],
    CPPPATH = ['$SOURCE_ROOT'],
    COMPONENT_LIBRARY_PUBLISH = True,
    SOURCE_ROOT = Dir('#/..').abspath
)

base_env.Prepend(CPPDEFINES = ['XP_UNIX'])

base_env.Append(BUILD_SCONSCRIPTS =
                ['npapi_pi_generator/npapi_pi_generator.scons',
                 'npapi_helloworld/npapi_helloworld.scons'])


#-----------------------------------------------------------------------------
# Build environment for NaCl module
platform = ''
if base_env['PLATFORM'] is 'darwin':
    platform = 'mac'
if base_env['PLATFORM'] is 'posix':
    platform = 'linux'

nacl_compiler_location = (
    '%s/compilers/host_%s/target_%s/sdk/nacl-sdk/' %
    (base_env['SOURCE_ROOT'], platform, GetOption('target')))

# This header path is required for files like pgl.h, which include "npapi.h"
# and not "nacl/npapi.h".
base_env.Append(CPPPATH = ['%s/nacl/include/nacl' % nacl_compiler_location])

#-----------------------------------------------------------------------------
# NaCl module build environment
nacl_env = base_env.Clone(
    tools = ['naclsdk'],
    BUILD_GROUPS = ['default'],
    BUILD_TYPE_DESCRIPTION = 'NaCl module',
    BUILD_TYPE = 'nacl',
    LIBS = ['google_nacl_imc',
            'google_nacl_npruntime',
            'pthread',
            'srpc'],
    NATIVE_CLIENT_SDK_DEFAULT_MODE='custom:' + nacl_compiler_location
)

environment_list.append(nacl_env)

#-----------------------------------------------------------------------------
# Build environment for trusted PINPAPI plugin on OS X
mac_dbg_plugin_env = base_env.Clone(
    BUILD_TYPE = 'plugin',
    BUILD_TYPE_DESCRIPTION = 'OS X trusted PINPAPI plugin debug build',
    BUILD_GROUPS = ['default'],
    CPPDEFINES = ['NPAPI_PLUGIN'],
    tools = ['applelink', 'target_platform_mac', 'target_debug']
)
mac_dbg_plugin_env.FilterOut(CCFLAGS = ['-g'])
mac_dbg_plugin_env.Append(BUILD_SCONSCRIPTS = ['trusted_gpu/trusted_gpu.scons'],
                          CCFLAGS = ['-gfull', '-O0'],
                          CPPPATH = ['$SOURCE_ROOT/third_party/include'])

environment_list.append(mac_dbg_plugin_env)

BuildComponents(environment_list)
