#!/usr/bin/python
#
# Copyright 2010 The Native Client SDK Inc. All Rights Reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

Import('env')

program_name = 'npapi_pi_generator'

npapi_pi_inputs = [
  'base_object.cc',
  'npp_gate.cc',
  'plugin.cc',
]

if env['BUILD_TYPE'] == 'nacl':
  nacl_module = '%s.nexe' % program_name
  env.ComponentProgram(nacl_module, npapi_pi_inputs)
  
  # Note that the html is required to run this program.
  env.Publish(nacl_module, 'run', ['npapi_pi.html',])
elif env['BUILD_TYPE'] == 'plugin' and env.Bit('mac'):
  # Build a Mac trusted plugin for debugging.
  mac_plugin_env = env.Clone(
      LINKFLAGS = ['-bundle', '-framework', 'Foundation']
  )
  # Build the plugin binary
  binary = mac_plugin_env.ComponentProgram(
      program_name,
      npapi_pi_inputs,
      COMPONENT_STATIC = False,
      COMPONENT_LIBRARY_PUBLISH=True,
      COMPONENT_LIBRARY_LINK_SUFFIXES=[]
  )
  RESOURCE_DIR = '%s/examples/npapi_pi_generator/mac' % env['SOURCE_ROOT']
  # Create resource fork
  # TODO(dspringer): old-school resource files are no longer needed on the Mac,
  # but the Environment.Bundle() function requires it.
  RESOURCE_DESC = '%s/npapi_pi_generator.r' % RESOURCE_DIR
  RESOURCE_FORK = 'npapi_pi_generator.rsrc'
  REZ = '/Developer/Tools/Rez'
  env.Command(
      target = RESOURCE_FORK,
      source = RESOURCE_DESC,
      action = [Action(REZ + ' -o ${TARGET} ${SOURCE} -useDF')]
  )
  PLIST_FILE = 'Info.plist'
  ACTION = ("cp -f ${SOURCE} ${TARGET};"
            "chmod +w ${TARGET};"
            "/usr/libexec/PlistBuddy -c 'Set :CFBundleExecutable npapi_pi_generator' ${TARGET};" 
            "/usr/libexec/PlistBuddy -c 'Set :CFBundleIdentifier com.google.npapi_pi_generator' ${TARGET};"
            "/usr/libexec/PlistBuddy -c 'Set :CFBundleName npapi_pi_generator' ${TARGET}")
  env.Command(
      target = PLIST_FILE,
      source = '%s/npapi_pi_generator-Info.plist' % RESOURCE_DIR,
      action = [Action(ACTION)]
  )

  plugin_name = '${STAGING_DIR}/%s.plugin' % program_name
  mac_plugin_env.Bundle(
      plugin_name,
      BUNDLE_EXE = binary[0],
      BUNDLE_INFO_PLIST = [PLIST_FILE],
      BUNDLE_PKGINFO_FILENAME = 0,
      BUNDLE_RESOURCES = RESOURCE_FORK
  )
  env.Publish(program_name, 'run',
              ['npapi_pi.html',
               'nacl_js_lib.js'])
