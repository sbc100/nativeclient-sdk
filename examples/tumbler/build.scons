#! -*- python -*-
#
# Copyright (c) 2011 The Native Client Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import make_nacl_env
import nacl_utils
import os

nacl_env = make_nacl_env.NaClEnvironment(
    use_c_plus_plus_libs=True, nacl_platform=os.getenv('NACL_TARGET_PLATFORM'))
nacl_env.Append(
    # Add a CPPPATH that enables the full-path #include directives, such as
    # #include "examples/sine_synth/sine_synth.h"
    CPPPATH=[os.path.dirname(os.path.dirname(os.getcwd()))],
    # Strict ANSI compliance.
    EXTRA_CCFLAGS=['-pedantic'],
    LIBS=['ppapi_gles2'],
   )

sources = [
    'cube.cc',
    'opengl_context.cc',
    'scripting_bridge.cc',
    'shader_util.cc',
    'transforms.cc',
    'tumbler.cc',
    'tumbler_module.cc',
    ]

opt_tumbler, dbg_tumbler = nacl_env.AllNaClModules(sources, 'tumbler')

# This target is used by the SDK build system to provide a prebuilt version
# of the example in the SDK installer.
nacl_env.InstallPrebuilt('tumbler')

# Install this web site in a staging area.  The staging area is determined by
# the NACL_INSTALL_ROOT environment variable.  By default, the staging area is
# C:\inetpub\wwwroot\tumbler
nacl_install_root = os.getenv(
    'NACL_INSTALL_ROOT', os.path.join('C:%s' % os.sep, 'inetpub', 'wwwroot'))
install_dir = os.path.join(nacl_install_root, 'tumbler')

app_files = [
    'tumbler.html',
    'tumbler.nmf',
    'bind.js',
    'dragger.js',
    'trackball.js',
    'tumbler.js',
    'vector3.js',
    ]

install_app = nacl_env.Install(dir=install_dir,
                               source=opt_tumbler + app_files)

nacl_env.Alias('install', source=[install_app])

