#!/usr/bin/python
#
# Copyright (c) 2010 The Ginsu Project Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

""" Build file for tests of c_salt code
"""

import os
import sys

Import('env')

sel_ldr = os.path.join(env['NACL_TOOLCHAIN_ROOT'], 'bin', 'sel_ldr')
if not os.path.exists(sel_ldr):
  sys.stderr.write('sel_ldr is not installed as part of the NaCl toolchain.\n')
  sys.exit(1)

small_test_inputs = ['c_salt_test_module.cc',
                     'callback_test.cc',
                     'notification_test.cc',
                     'notification_center_test.cc',
                     'npapi/variant_converter_test.cc',
                     'variant_test.cc',
                    ]

env.ComponentTestProgram(
    'small_c_salt_test',
    small_test_inputs,
    COMPONENT_TEST_CMDLINE = '%s $PROGRAM_NAME' % sel_ldr,
    COMPONENT_TEST_SIZE = 'small'
)

# Note, we can't use the default test environment libraries, because gtest
# injects a main() function that makes the nexe not work as a plugin module.
integration_test_input_libs = [
  'google_nacl_imc',
  'google_nacl_npruntime',
  'pthread',
  'srpc',
  'c_salt',
  'google_nacl_pgl',
  'google_nacl_gpu',
]
integration_test_inputs = ['integration_tests/fake_instance.cc',
                           'integration_tests/method_tester.cc',
                           'integration_tests/property_tester.cc',
                           'integration_tests/test_module.cc',
]
integration_test_app = [
    'integration_tests/c_salt_tests.html',
]

install_integration_test_app  = env.Install('$STAGING_DIR',
    integration_test_app)
# TODO(dmichael):  Is there an appropriate test type for this, instead of
#                  making the target a ComponentProgram?
integration_test = env.ComponentProgram(
    'c_salt_integration_test_x86_32.nexe',
    integration_test_inputs,
    LIBS = integration_test_input_libs,
)
env.Depends(integration_test, install_integration_test_app)
