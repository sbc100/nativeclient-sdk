#! -*- python -*-
#
# Copyright (c) 2011 The Native Client Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Note: flocking_geese requires libpng, libjpeg and boost to be installed into
# the SDK.  You can do this by checking out naclports.googlecode.com, and
# doing make png; make jpeg; make boost.  This doesn't work on Windows (or
# requires cygwin).

import make_nacl_env
import nacl_utils
import os

nacl_platform_dir = os.path.join(os.getenv('NACL_SDK_ROOT', ''),
                                 os.getenv('NACL_TARGET_PLATFORM', ''))

nacl_env = make_nacl_env.NaClEnvironment(
    use_c_plus_plus_libs=True, nacl_platform=os.getenv('NACL_TARGET_PLATFORM'))
nacl_env.Append(
    CPPPATH=[
        os.getcwd(),
        os.path.dirname(os.getcwd()),
        os.path.join(nacl_platform_dir, 'third_party'),
        ],
    OPT_CCFLAGS=['-ffast-math',
                 '-mfpmath=sse',
                 '-msse2'],
    LIBS=['jpeg', 'png', 'z'],
   )

app_sources = [os.path.join('nacl_app', src_file) for src_file in [
    'flock.cc',
    'flocking_geese_app.cc',
    'flocking_geese_module.cc',
    'frame_counter.cc',
    'goose.cc',
    'png_loader.cc',
    'sprite.cc',
]]

scripting_sources = [os.path.join('scripting', src_file) for src_file in [
   'scripting_bridge.cc',
]]

url_io_sources = [os.path.join('url_io', src_file) for src_file in [
    'web_resource_loader.cc',
    'url_request.cc',
]]

sources = app_sources + scripting_sources + url_io_sources

opt_nexes, dbg_nexes = nacl_env.AllNaClModules(sources, 'flocking_geese')

install_dir = os.getenv('INSTALL_ROOT',
                        os.path.join(os.path.expanduser('~'),
                                     'Sites',
                                     'flocking_geese'))

cws_files = ['manifest.json',
             os.path.join('images', 'flocking_geese_icon.png'),
             os.path.join('images', 'flocking_geese_icon16.png'),
           ]

install_cws = nacl_env.Install(dir=os.path.join(install_dir, 'cws'),
                               source=cws_files)

app_files = ['about.html',
             'flocking_geese.nmf',
             'google0bfe793046475826.html',
             'index.html',
            ]

install_app = nacl_env.Install(dir=install_dir,
                               source=opt_nexes + app_files)

js_files = [os.path.join('js', js_file) for js_file in [
    'check_browser.js',
    'closure.js',
    'flock.js',
    'flocking_geese.js',
    'frame_counter.js',
    'goose.js',
    'load_progress.js',
    'slider.js',
    'slider_event.js',
    'speedometer.js',
]]

install_js = nacl_env.Install(
    dir=os.path.join(install_dir, 'js'),
    source=js_files)

css_files = [os.path.join('css', css_file) for css_file in [
    'flocking_geese.css',
    'form_input.css',
    'info_panel.css',
    'load_progress.css',
    'slider.css',
    'speedometer.css',
]]

install_css = nacl_env.Install(
    dir=os.path.join(install_dir, 'css'),
    source=css_files)

image_files = [os.path.join('images', img_file) for img_file in [
    'DialBackground.png',
    'DialFace.png',
    'Flock32_Green.png',
    'Flock32_red.png',
    'flock_size_ruler.png',
    'hub.png',
    'numbers_grey.png',
    'numbers_red.png',
    'odometer.png',
    'pointer_green.png',
    'pointer_grey.png',
    'pointer_red.png',
    'sim_select_ruler.png',
    'slider_thumb.png',
]]

install_images = nacl_env.Install(
    dir=os.path.join(install_dir, 'images'),
    source=image_files)

nacl_env.Alias('install',
               source=[install_cws,
                       install_app,
                       install_css,
                       install_js,
                       install_images])

